# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
_srcname=skinElchi
pkgname=skindesigner-$(echo $_srcname | tr '[:upper:]' '[:lower:]')
pkgver=0.0.1_22_g585107c
_gitver=585107c03a5a3daeca967fc13177d1dac86a4c07
pkgrel=1
pkgdesc="ElchiAIO Skin for SkinDesigner"
arch=('any')
url="https://github.com/CReimer/skinElchi"
depends=('vdr-skindesigner')
makedepends=('git')
optdepends=('skindesigner-logos: To show channellogos in DisplayChannel view')
license=('GPL2')
source=("git://github.com/CReimer/skinElchi.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_srcname}"
  git tag -a 0.0.1 -m 'Added Tag' 9f8a4d431a631433e5ef7e8930ca1ee041e879dd 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

package() {
  cd "$srcdir/${_srcname}"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/$_srcname"
  cp -r * "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/$_srcname"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  find $pkgdir -name *.theme -exec mv {} "$pkgdir/var/lib/vdr/themes" \;

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
