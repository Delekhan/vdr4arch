# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
_srcname=shady
pkgname=skindesigner-$(echo $_srcname | tr '[:upper:]' '[:lower:]')
_gitver=b1e32101d6703b2e74bfac6423b901ec53b50be2
pkgver=20150525
pkgrel=1
pkgdesc="Shady Skin for SkinDesigner"
arch=('any')
url="http://www.anthra.de/shady-skin"
depends=('ttf-opensans' 'vdr-skindesigner')
optdepends=('skindesigner-logos: To show channellogos in DisplayChannel view')
license=('GPL2')
source=("git://github.com/tomsax56/shady.git#commit=$_gitver"
        'shady-use-opensans-font.diff')
md5sums=('SKIP'
         '98837f84adae023465616837ac38d2f4')

pkgver() {
  cd "$srcdir/${_srcname}"
  git log -1 --date=short --format=%cd | tr -d -
}

prepare() {
  cd "$srcdir/${_srcname}"
  patch -p1 -i "$srcdir/shady-use-opensans-font.diff"
}

package() {
  cd "$srcdir/${_srcname}"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/$_srcname"
  rm COPYING HISTORY README *.orig
  cp -r * "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/$_srcname"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  find $pkgdir -name *.theme -exec mv {} "$pkgdir/var/lib/vdr/themes" \;

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
