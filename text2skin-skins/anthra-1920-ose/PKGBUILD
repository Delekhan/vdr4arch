# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
_srcname=anthra_1920_OSE
pkgname=text2skin-$(echo ${_srcname//_/-} | tr '[:upper:]' '[:lower:]')
pkgver=0.0.5
pkgrel=2
pkgdesc="Dark, anthracite skins for Klaus Schmidingers VDR"
arch=('any')
url="http://www.anthra.de/"
depends=('vdr-text2skin')
groups=('text2skin-anthra')
license=('GPL2')
source=("http://www.anthra.de/anthra-skins/$_srcname-$pkgver.tar.xz")
md5sums=('4aa8a8f177511374124a2787aae91a61')

prepare() {
  cd "$srcdir/$_srcname"
  sed -i 's/var\/lib\/vdr/usr\/share\/vdr\/plugins\/text2skin/g' $_srcname.skin
}

package() {
  cd "$srcdir/$_srcname"
  make DESTDIR="$pkgdir" THEMESDIR="/var/lib/vdr/themes" install
  find "$pkgdir" -name COPYING -delete
  find "$pkgdir" -name .directory -delete

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
