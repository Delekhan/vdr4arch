# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>

pkgname=poco
pkgver=1.6.0
pkgrel=1
pkgdesc="C++ class libraries for network-centric, portable applications, complete edition"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://www.pocoproject.org"
license=('custom:boost')
depends=('expat' 'libmariadbclient' 'sqlite' 'unixodbc')
makedepends=('chrpath')
source=("http://pocoproject.org/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}-all.tar.gz"
        'poco-1.6.0-patch-for-libpcre.diff')
md5sums=('4fed893d4ca57db98b0e10d82545232a'
         '04bb7e95f2def859312903995db17593')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}-all"
  patch -p1 -i "$srcdir/poco-1.6.0-patch-for-libpcre.diff"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-all"
  ./configure --prefix=/usr --no-samples --no-tests --unbundled
  make ODBCLIBDIR="/usr/lib"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-all"
  make ODBCLIBDIR="/usr/lib" DESTDIR="${pkgdir}" install
  install -Dm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # remove rpath information from binaries
  chrpath -d "${pkgdir}/usr/bin/cpspc"
  chrpath -d "${pkgdir}/usr/bin/cpspcd"
  chrpath -d "${pkgdir}/usr/bin/f2cpspd"
  chrpath -d "${pkgdir}/usr/bin/f2cpsp"

}
